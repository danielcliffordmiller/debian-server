#!/usr/bin/expect --

set kadmin_prompt "kadmin.local:  "

proc list_principals {spawn_id} {
    global kadmin_prompt

    send "list_principals\r"

    expect "^list_principals\r\n"

    expect {
        -re "^(\[^\\r]*)\r\n" {
            lappend principals $expect_out(1,string)
            exp_continue
        }
        "^$kadmin_prompt"
    }

    return $principals
}

proc add_principal {spawn_id principal password} {
    global kadmin_prompt

    send "add_principal $principal\r"

    expect "Enter password for principal \"*\""
    send "$password\r"

    expect "Re-enter password for principal \"*\""
    send "$password\r"

    expect $kadmin_prompt
}

spawn -noecho kadmin.local

expect $kadmin_prompt

set principals [list_principals $spawn_id]

if {[lsearch $principals "danmiller*"] == -1} {
    add_principal $spawn_id "danmiller" "qwerty"
}

if {[lsearch $principals "danmiller/admin*"] == -1} {
    add_principal $spawn_id "danmiller/admin" "qwerty2"
}

send "quit\r"

expect eof
